# Maintainer: Dominic Sisneros <dsisnero@gmail.com>

_realname=lasem
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.1
pkgrel=1
pkgdesc="Lasem aims to be a C/Gobject based SVG/Mathml renderer (mingw-w64)"
arch=('any')
url="https://github.com/GNOME/lasem"
license=('LGPL')
license=('GPL' 'MPL' 'LGPL')
makedepends=(
            "automake"
            "perl"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-pango"
             "${MINGW_PACKAGE_PREFIX}-libxml2"
             "flex"
             "bison"
             "intltool"
            )
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/GNOME/lasem/archive/LASEM_${pkgver//./_}.tar.gz"
        001-fix-doc-install.patch
        002-no-undefined.patch
        003-add-mathml-headers.patch)
sha256sums=('896e8a778238daeb09a4f3f1291bc5e2d457494a9a3dba78c4b419d4a3dd8233'
            '9c9321e4f2c841d3b348204519b5e508492521f4c2c512ecfb8a083a06dca1e3'
            '7f98e6d191c53ffe80235258c5114c78e3e7af889c5c07c79ed4134e2be7e3b8'
            'eb0fa95879ca077076a5673e1a96327cf5c63f1d1962a6e143f25c1200781e2a')


prepare() {
  cd "${_realname}-LASEM_${pkgver//./_}"
  patch -p1 -i ${srcdir}/001-fix-doc-install.patch
  patch -p1 -i ${srcdir}/002-no-undefined.patch
  patch -p1 -i ${srcdir}/003-add-mathml-headers.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-LASEM_${pkgver//./_}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --enable-gtk-doc

  make
}

# check() {
#  #   make check
# }

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" -j1 install

  install -Dm644 ${srcdir}/${_realname}-LASEM_${pkgver//./_}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
